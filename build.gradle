repositories {
    mavenCentral()
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'signing'

targetCompatibility='1.8'
sourceCompatibility='1.8'

group 'org.freeplane'
version='0.8'


dependencies {
    implementation gradleApi()
    implementation localGroovy()
}

task sourceJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'gradle-freeplane-plugin'
            artifact jar
            artifact sourceJar
			artifact javadocJar
            pom {
                name = project.name
                description = 'Gradle Plugin for Freeplane Script and Add-on Development'
                url = 'https://github.com/freeplane/gradle-plugin'

                scm {
                    url = 'https://github.com/freeplane/gradle-plugin'
                    connection = 'scm:git:https://github.com/freeplane/gradle-plugin.git'
                    developerConnection = 'scm:git:git@github.com:freeplane/gradle-plugin.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'dpolivaev'
                        name = 'Dimitry Polivaev'
                        email = 'dpolivaev@gmx.de'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username project.findProperty('ossrhUsername') ?: '-'
                password project.findProperty('ossrhPassword') ?: '-'
            }
        }
        maven { 
			 url  = "${project.gradle.gradleUserHomeDir}/local-artifacts"
		}
    }
}

signing {
    sign publishing.publications.maven
}
